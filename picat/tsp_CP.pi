%% TSP usando MATRIZ de DECISAO
%%
import cp,util.
import matrix_38.
%import matrix_7.
/*
matriz_adj(Matrix) =>
   Matrix = 
       [[ 0, 4, 8,10, 7,14,15],
        [ 4, 0, 7, 7,10,12, 5],
        [ 8, 7, 0, 4, 6, 8,10],
        [10, 7, 4, 0, 2, 5, 8],
        [ 7,10, 6, 2, 0, 6, 7],
        [14,12, 8, 5, 6, 0, 5],
        [15, 5,10, 8, 7, 5, 0]].
*/

main => 
  matriz_adj(Matriz), %% leitura dos dados
  %tsp_D( Inp, Out, Out, Out),
  tsp_D( Matriz, Cidades, M_Decisao, Custo),
  
  printf("\nM_Decisao: %w\n", M_Decisao),
  print_matrix( M_Decisao ),
  printf("Sequência das Cidades: %w\n", Cidades),
  printf("Custo: %w\n", Custo),
  show_tour(Cidades, Matriz) .
%
% TSP using a matrix circuit/1 constraints.
% 
%
tsp_D(Matriz, Cidades, M_Decisao, Custo) =>

   Len = Matriz.length, %% N x N cidades
   % printf("N cidades: %d", Len),

   Cidades = new_list(Len), %%% UMA dimensao
   Cidades :: 1..Len,
   % grafo de DECISAO que representa o resultado dos nos escolhidos
   M_Decisao = new_array (Len, Len),
   M_Decisao :: 0..1 ,
   
   % calculate upper and lower bounds of the Costs list -- HAKAN
   % repensar MELHORAR .....
   SOMA_Dists = sum([Matriz[I,J] : I in 1..Len, 
                J in 1..Len, Matriz[I,J] > 0]),
   MinDist = 0,
   MaxDist = SOMA_Dists,
   printf("\n ...DISTS %d", SOMA_Dists),
   Custo :: 0..MaxDist,
   
   
% Aceitar somente arcos como resultados válidos aqueles 
% existentes no GRAFO ORIGINAL
% Se NAO HOUVER CONEXAO ou  ARCO = 0 entao não há conexão
  foreach(I in 1..Len , J  in 1..Len)
    (Matriz[I,J] #= 0) #=> (M_Decisao[I,J] #= 0)
  end,    

% Para todas linhas, a soma das colunas é igual a 1    
% Todo noh tem um proximo no, ou UMA aresta .. ex: 3 -> 4
% e não 3 --> 5, 3 --> 6 ... uma escolha de saída
% UMA: uma saida como caminho a ser traçado
   foreach(I in 1..Len)
      sum([M_Decisao[I,J] : J in 1..Len, I != J]) #= 1
   end,     
  
% Para todas colunas, a soma das linhas é igual a 1    
% TODO noh destino de algum outro no .. ex: 5 -> 3  ...
%% e  nao podera ter mais 4 -> 3, 6 -> 3
% UMA: uma chegada ao nó de destino e somente 1 chegada
  foreach(J in 1..Len)
   sum([M_Decisao[I,J] : I in 1..Len, I != J]) #= 1
  end,     

  %% Relacionar as escolhas da M_Decisao com a
  %% Sequencia das Cidades. 
  foreach(I in 1..Len , J  in 1..Len)
    ( M_Decisao[I,J] #= 1 ) #<=> ( Cidades[I] #= J )
  end,    
 
  %% garante o circuito entre os nós selecionados
  circuit(Cidades),
  
 %% Função custo a ser minimizada
  Custo #= sum([M_Decisao[I,J] * Matriz[I,J] : 
                I in 1..Len , J  in 1..Len]),

  %% Vars para BUSCA
  Vars = [Cidades, M_Decisao], %% OU  Cidades ++ M_Decisao
  solve([$min(Custo)], Vars ).
   
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Codigos análogos da linguagens imperativas
print_matrix( M ) =>
  L = M.length,
  C = M[1].length,
  printf("DESTINOS:\n      "),
  foreach(J in 1  .. C)
    printf("%d ", J)
  end,
  nl,
  foreach(I in 1  .. L)
      printf(" %d -> ", I),
      foreach(J in 1  ..  C)
      printf("%d " , M[I,J] )
    %% printf("(%d,%d): %w " , I, J, M[I,J] ) -- OK
      end,
    nl
  end.   
  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Codigos análogos da linguagens imperativas
% print the tour
% Hakan e CCS
show_tour(Cidades, Matriz_Custo) =>
   City = 1, % start from city 1
   N = 0,
   S_parcial = 0,
   printf("\n A viagem: \n"),
   while(N < Cidades.len) 
     City2 = Cidades[City],
     Cost2 := Matriz_Custo[City,City2],
     S_parcial := S_parcial + Cost2,
     printf("Da cidade %d --> %d custa: %d\t Acumulado: %d\n", 
             City,City2,Cost2, S_parcial),
     N := N + 1,
     City := City2
   end,
   nl.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

/*
  ESTUDE a equivalencia   
   foreach(I in 1..Len , J  in 1..Len)
       (Matriz[I,J] #= 0) #=> (M_Decisao[I,J] #= 0)
      %%%(Matriz[I,J] #!= 0) #=> (M_Decisao[I,J] #= 1)
       
       OU ---> if(Matriz[I,J] == 0) then
       M_Decisao[I,J] #= 0
       else
       M_Decisao[I,J] #= 1
       end
      
    end,     

*/
    
